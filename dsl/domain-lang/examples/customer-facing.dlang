Domain Enterprise {
    description: "Enterprise portfolio serving all customer touchpoints"
    vision: "Create a cohesive experience from discovery to fulfillment"
}

Namespace CustomerFacing {
    Classification CoreDomain
    Classification SupportingDomain
    Team PlatformGuild
    Team ProductTeam

    Domain Inventory in Enterprise {
        description: "Catalog definitions, availability, and channel policies"
        classification: SupportingDomain
    }

    BoundedContext ApplicationFramework for Enterprise as SupportingDomain by PlatformGuild {
        description: "Shared UI primitives and orchestration for edge experiences"
        terminology {
            term MicroFrontend: "Self-contained UI vertical integrated at runtime"
                aka: MicroApp
        }
        decisions {
            decision [technical] AdoptModuleFederation: "Use module federation for runtime composition"
        }
    }

    BoundedContext Listings for Inventory {
        description: "Customer-facing product discovery and merchandising"
        role: CoreDomain
        terminology {
            term Product: "An item that can be purchased"
                examples: "Laptop #12345", "Mouse #67890"
            term SKU: "Stock keeping unit identifier"
        }
        decisions {
            policy NoBackorders: "Do not allow orders without available stock"
            rule [compliance] ProductMustHaveSku: "Every product requires a SKU for audit trails"
        }
    }

    ContextMap WebExperience {
        contains ApplicationFramework, Listings
        [SK] ApplicationFramework <-> Listings : SharedKernel
        [OHS] Listings -> [ACL] ApplicationFramework
    }
}